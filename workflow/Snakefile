from pathlib import Path

import pandas as pd
from snakemake.utils import min_version

min_version("8.25.0")

# Load config
configfile: "config/config.yaml"

# Load sample sheet
samplesheet_df = pd.read_csv(config["sample_sheet"])

# Ensure sample ID is unique as there is no concatenation step
if samplesheet_df["sample_id"].nunique() != samplesheet_df["sample_id"].size:
    sample_duplicates = samplesheet_df['sample_id'][samplesheet_df['sample_id'].duplicated()]
    raise ValueError(f"Sample IDs are not unique {sample_duplicates}")

# Needed for analyses creating a summary from the samples such as multiqc_raw
samples = samplesheet_df["sample_id"].tolist()

comparisons_file = config["comparison_sheet"]
comparisons_df = pd.read_csv(comparisons_file)
comparisons = comparisons_df.apply(
    lambda row: f"{row['treatment']}_vs_{row['control']}",
    axis=1
).tolist()


# Final report output
report: "report/workflow.rst"

include: "rules/common.smk"

# Download the genome data
include: "rules/download_reference_files.smk"

# Additional reference-based annotation
include: "rules/create_rseqc_annotation.smk"

##################################
# QC and Trimming
##################################
include: "rules/detect_adapters.smk"
include: "rules/fastqc_raw.smk"
include: "rules/multiqc_raw.smk"
include: "rules/trimming.smk"
include: "rules/fastqc_trimmed.smk"
include: "rules/multiqc_trimmed.smk"

##################################
# Alignment and Quantification
##################################
include: "rules/alignment_star.smk"
include: "rules/index_bam.smk"
include: "rules/samtools_stats.smk"
include: "rules/mosdepth.smk"
include: "rules/multiqc_bam.smk"
include: "rules/quantify_transcripts_salmon.smk"
include: "rules/combine_expression_matrix.smk"
include: "rules/extract_bam_stats.smk"
include: "rules/gene_body_coverage.smk"
include: "rules/infer_experiment.smk"
include: "rules/read_distribution.smk"
include: "rules/read_duplication.smk"
include: "rules/rpkm_saturation.smk"
include: "rules/junction_saturation.smk"
include: "rules/bam_stat.smk"
include: "rules/process_genome_content.smk"
include: "rules/go_enrichment.smk"
include: "rules/deseq_analysis_comparison.smk"

rule all:
    input:
        # Files which are created for the last analyses, needed for Snakemake to
        # understand what is needed to be run
        f"results/multiqc/trimmed/multiqc_report.html" if config["trim_reads"] else [],
        f"results/multiqc/raw/multiqc_report.html",
        f"results/multiqc/bam/multiqc_report.html",
        expand("results/enrichment/GO/Biological_Process/{comparison}_biological_process_enrichment.csv", comparison=comparisons) if config["do_comparison"] else "results/Quant/Count/combined_expression.csv",
        expand("results/enrichment/GO/Molecular_Function/{comparison}_molecular_function_enrichment.csv", comparison=comparisons) if config["do_comparison"] else [],
        expand("results/enrichment/GO/Cellular_Component/{comparison}_cellular_component_enrichment.csv", comparison=comparisons) if config["do_comparison"] else [],
